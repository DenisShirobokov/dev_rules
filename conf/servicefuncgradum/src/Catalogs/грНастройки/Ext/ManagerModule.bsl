 
#Область ПрограммныйИнтерфейс

// Формирует текст запроса шаблон для получения настройки.
// Используется для получения значения настройки в запросах.
// В зппрос добавляется шаблок с указанием имени нстройки, в дальнейшем вызывается функцкция ЗапросПоПредставлению, которая заменяет шаблок 
// на текст запроса конкртеной настройки.
//	
//	Параметры: 
//		Нет. 
//		
//	Возвращаемое значение:
//		Строка - Шаблон текста запроса
Функция СтруктураДанныхНастройки() Экспорт 
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Значение
	|ПОМЕСТИТЬ %ПредставлениеНастройки%_ИМЯ_НАСТРОЙКИ
	|";
	
	ТекстЗапроса = СтрЗаменить( ТекстЗапроса, "%ПредставлениеНастройки%", ПрефиксПредставленияТаблицыЗапроса() );
				
	Возврат ТекстЗапроса;
	
КонецФункции

// Выполняет замену шаблонов текста запроса по настройкам на текст запрос получения данных.
//	
//	Параметры: 
//		ТекстЗапроса - Строка. Текст запроса, в котором выполняется замена шаблона.
//		
//	Возвращаемое значение:
//		Строка - Тест запроса для исполнения.
Функция ЗапросПоПредставлению( ТекстЗапроса )  Экспорт
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса( ТекстЗапроса );
	
	ПрефиксИмениТаблицы = ПрефиксПредставленияТаблицыЗапроса() ;
	
	Для Каждого ПакетЗапроса Из СхемаЗапроса.ПакетЗапросов Цикл
			
		
		Если СтрНайти( ВРег( ПакетЗапроса.ТаблицаДляПомещения ), ВРег( ПрефиксИмениТаблицы ) ) > 0 Тогда
			
			ИмяНастройки = Прав( ПакетЗапроса.ТаблицаДляПомещения,
								СтрДлина( ПакетЗапроса.ТаблицаДляПомещения ) - СтрДлина( ПрефиксИмениТаблицы ) );
									
			ПакетЗапроса.УстановитьТекстЗапроса( ТекстЗапросаНастройки( ИмяНастройки, Истина ) );
			
		КонецЕсли;
				
	КонецЦикла;
	   	
	Возврат СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецФункции

// Возвращает значение настройки по имени.
//	
//	Параметры: 
//		ИмяНастройки - Строка. 
//		
//	Возвращаемое значение:
//		ЗначениеНастройки - произвольный тип
Функция ПолучитьЗначениеНастройки( ИмяНастройки )  Экспорт
	
	Запрос = Новый Запрос;
	 	 	
	Запрос.Текст = ТекстЗапросаНастройки( ИмяНастройки );
		
	ТЗЗначений = Запрос.Выполнить().Выгрузить();
	
	Если ТЗЗначений.Количество() = 0 Тогда
		Результат = Неопределено;
	ИначеЕсли ТЗЗначений.Количество() = 1 Тогда
		Результат = ТЗЗначений[ 0 ].Значение;
	Иначе
		Результат = ТЗЗначений.ВыгрузитьКолонку( "Значение" );
	КонецЕсли;
		
	Возврат Результат;
		
КонецФункции //ПолучитьЗначение()

#КонецОбласти  //ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Генерация текста запроса для получения значения настройки по имени.
//	
//	Параметры: 
//		ИмяНастройки - Строка.
//      ПоместитьВоВременнуюТаблицу - Булево. Если Истина, то результат запроса помещается во временную таблицу. 
//		
//	Возвращаемое значение:
//		Строка 
Функция ТекстЗапросаНастройки( ИмяНастройки, ПоместитьВоВременнуюТаблицу = Ложь ) 
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	грНастройки.Значение КАК Значение
	|ИЗ
	|	Справочник.грНастройки КАК грНастройки
	|ГДЕ
	|	грНастройки.Ссылка = &ОтборСсылка
	|	И НЕ грНастройки.ЗначениеСписком
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	грНастройкиЗначения.Значение
	|ИЗ
	|	Справочник.грНастройки.Значения КАК грНастройкиЗначения
	|ГДЕ
	|	грНастройкиЗначения.Ссылка = &ОтборСсылка
	|	И грНастройкиЗначения.Ссылка.ЗначениеСписком
	|
	|СГРУППИРОВАТЬ ПО
	|	грНастройкиЗначения.Значение";
	
	
	ЗначениеОтборВЗапрос = СтрШаблон( "ЗНАЧЕНИЕ( Справочник.грНастройки.%1 )", ИмяНастройки );
	ТекстЗапроса = СтрЗаменить( ТекстЗапроса, "&ОтборСсылка", ЗначениеОтборВЗапрос );
	
	Если ПоместитьВоВременнуюТаблицу  Тогда
		
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса( ТекстЗапроса );
		СхемаЗапроса.ПакетЗапросов[0].ТаблицаДляПомещения = ПрефиксПредставленияТаблицыЗапроса() + ИмяНастройки;
		ТекстЗапроса  = СхемаЗапроса.ПолучитьТекстЗапроса();
		
	КонецЕсли;
	
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает префикс имени таблицы шаблона настройки.
//	
//	Параметры: 
//		Нет. 
//		
//	Возвращаемое значение:
//		Строка
Функция ПрефиксПредставленияТаблицыЗапроса()
	
	 Возврат "Представления_Настройки_";
	
КонецФункции


#КонецОбласти   //СлужебныеПроцедурыИФункции